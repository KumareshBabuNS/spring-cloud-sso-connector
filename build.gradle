buildscript {
  repositories {
    maven { url "https://repo.spring.io/plugins-release" }
    maven { url "https://plugins.gradle.org/m2/" }
    mavenCentral()
  }

  dependencies {
    classpath("org.springframework.build.gradle:propdeps-plugin:0.0.7")
    classpath("io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE")
    classpath("io.spring.gradle:spring-io-plugin:0.0.8.RELEASE")
  }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'maven'

repositories {
    mavenCentral()
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-starter-parent:Dalston.RELEASE"
  }
}

javadoc {
  options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
  options.author = true
  options.header = project.name
}

task packageSources(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
  classifier = "javadoc"
  from javadoc
}

artifacts {
    archives packageSources
    archives javadocJar
}

dependencies {
  compile 'org.springframework.cloud:spring-cloud-starter-oauth2'
  compile 'org.springframework.cloud:spring-cloud-spring-service-connector'
  compile 'org.springframework.cloud:spring-cloud-cloudfoundry-connector'
  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  testCompile group: "org.springframework.cloud", name: "spring-cloud-cloudfoundry-connector", version: "1.1.1.RELEASE", classifier: "tests"
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0.1'
}

archivesBaseName = "spring-cloud-sso-connector"
group = "io.pivotal.spring.cloud"

install {
    repositories.mavenInstaller {
        pom.project {
            name 'Spring Cloud SSO Connector'
            description 'Spring Cloud Connector Extension for Single Sign-On Service on Pivotal Cloud Foundry'
            url 'https://github.com/pivotal-cf/spring-cloud-sso-connector/'
            inceptionYear '2015'
            licenses {
                license {
                    name 'The Apache License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'jar'
                }
            }
            scm {
                connection 'scm:git:git@github.com:pivotal-cf/spring-cloud-sso-connector.git'
                developerConnection 'scm:git:git@github.com:pivotal-cf/spring-cloud-sso-connector.git'
                url 'https://github.com/pivotal-cf/spring-cloud-sso-connector/'
            }
            developers {
                developer {
                    id 'cdutra'
                    name 'Chris Dutra'
                    roles{
                        role 'Developer'
                    }
                }
                developer {
                    id 'mbhave'
                    name 'Madhura Bhave'
                    roles {
                        role 'Developer'
                    }
                }
                developer {
                    id 'lechang'
                    name 'Leslie Chang'
                    roles {
                        role 'Developer'
                    }
                }
            }
        }
    }
}
